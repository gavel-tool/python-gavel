%import common.WS
%import common.SIGNED_NUMBER
%import common.NUMBER
%ignore WS

DOLLAR_WORD: "$" WORD
WORD: /(\w|\$)+/
UPPER_WORD: ("A" .. "Z") WORD?
LOWER_WORD: LOWER WORD?
LOWER: "a" .. "z"

?typed_variable: variable [":"  type_binary_formula ]
variable:  UPPER_WORD
constant: LOWER_WORD
DECIMAL_NUMBER: SIGNED_NUMBER ("/" NUMBER)?

distinct_object: /"([^"]|((?<=\\)"))+"/
?single_quoted:  /'([^']|((?<=\\)'))+'/
?any_word: single_quoted | WORD | DOLLAR_WORD
?functor: LOWER_WORD | DOLLAR_WORD | single_quoted


OBJECT_BINARY_OPERATOR: "=" | "!="
TYPE_BINARY_OPERATOR: "@" | "*" | "+" | "-->" | ":=" | ">"
LOGIC_BINARY_OPERATOR: "=>" | "<=>" | "<=" | "<~>" | "~&" | "~|"

UNARY_CONNECTIVE: "~"
QUANTIFIER: "!" | "?"

?formula: disjunction

?disjunction: conjunction ("|" conjunction)*
?conjunction: logic_binary_formula ("&" logic_binary_formula)*
?logic_binary_formula: type_binary_formula (LOGIC_BINARY_OPERATOR type_binary_formula)*
?type_binary_formula: unary_formula (TYPE_BINARY_OPERATOR unary_formula)*
?unary_formula: (UNARY_CONNECTIVE unary_formula) | transition_formula
?transition_formula: object_binary_formula | "(" formula ")" | quantified_formula
quantified_formula: QUANTIFIER "[" (typed_variable ("," typed_variable)*) "]" ":" unary_formula
?object_binary_formula: term (OBJECT_BINARY_OPERATOR term)*

?term: functor_term | bottom | DECIMAL_NUMBER | distinct_object
functor_term: functor ["(" term ("," term)* ")"] [":"  type_binary_formula ]
?bottom: variable

LOGIC: "thf" | "tff" | "ttf" | "cnf" | "fof"

inference_rule: WORD "(" WORD ("," WORD )* ")"

inference_item: WORD | inference_rule

intro_type: "definition" | "axiom_of_choice" | "tautology" | "assumption"

//inference_source: "inference(" WORD "," "[" inference_item ("," inference_item )*  "]"  "," "[" source "]" ")"
//internal_source: "introduced(" WORD ( "," sources )? ")"
//any_word ( "(" source ("," source )* ")" )? | ((any_word ":")?)
//sources: "["  source ("," source)* "]"
// ?source: file_source // | inference_source | internal_source |  sources | /(?!(inference|introduced|file))\w+/

file_source: "file(" single_quoted [ "," (WORD | single_quoted) ] ")"
generic_annotation: /(?!\s*file\()(([^)]|\)(?!\.))+)/sx
?source: file_source

annotation: ( source ["," generic_annotation ] | generic_annotation )

FORMULA_ROLE: "axiom" | "hypothesis" | "definition" | "assumption" | "lemma" | "theorem" | "corollary" | "conjecture" | "negated_conjecture" | "plain" | "type" | "fi_domain" | "fi_functors" | "fi_predicates" | "unknown"

annotated_formula: LOGIC "(" ( WORD | single_quoted ) "," FORMULA_ROLE "," formula [ "," annotation ] ")."
include: "include(" "'" /[^']+/ "'" ( "," "[" WORD ("," WORD)* "]" )? ")."

%ignore COMMENT
%ignore BLOCK_COMMENT

COMMENT: /%[^\n]*(\n|$)/
BLOCK_COMMENT: /\/\*([^*]|\*(?!\/))*\*\//m
?tptp_line: annotated_formula | include | COMMENT | BLOCK_COMMENT

start: tptp_line*
